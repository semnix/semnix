# SPDX-FileCopyrightText: 2023 Christina Sørensen
# SPDX-FileContributor: Christina Sørensen
#
# SPDX-License-Identifier: AGPL-3.0-only

# Pulls each Submodule.
pullEach:
    git submodule foreach git pull

# Bump all submodules with upstream.
bumpFromUpstream:
    just pullEach
    just flake-utils
    just naersk
    git submodule status

bumpRepo REPO:
    just bumpFromUpstream
    just bumpRepoBash {{REPO}}

bumpRepoBash REPO:
    #!/usr/bin/env bash
    set -euxo pipefail
    # Ensure we have the latest and greatest
    cd repos/{{REPO}}

    gh repo set-default "semnix/{{REPO}}"
    new_version="$(convco version --bump)"
    git tag -d "v$new_version" || echo "tag not found, creating";
    git tag -a "v$new_version" -m "auto generated by semnix for {{REPO}} v$(convco version)"
    gh release create "v$(convco version)" --target "$(git rev-parse HEAD)" --title "{{REPO}} v$(convco version)" -d --generate-notes

    cd ../..


# --- Upstream Helpers
# These are DRY DRY DRY, should be made into a single recipe

# Ensures a upstream remote exists and rebase it on top of submodule.
flake-utils:
    #!/usr/bin/env bash
    set -euxo pipefail
    cd repos/flake-utils
    # We ignore errors because it's likely it already exists
    git remote add upstream git@github.com:numtide/flake-utils.git && echo "added upstream" || echo "upstream already exists";
    git fetch upstream
    git rebase upstream/main
    git push
    cd ../..

# Ensures a upstream remote exists and rebase it on top of submodule.
naersk:
    #!/usr/bin/env bash
    set -euxo pipefail
    cd repos/naersk
    # We ignore errors because it's likely it already exists
    git remote add upstream git@github.com:nix-community/naersk.git && echo "added upstream" || echo "upstream already exists";
    git fetch upstream
    git rebase upstream/master
    git push
    cd ../..

# ---
